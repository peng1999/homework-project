%code requires {
#include "ast.h"
#include "parse.h"
}

%{
#include <cstdio>
#include <iostream>
extern int yylex();
%}

%defines

%union {
    ast_node *a;
    double d;
    std::string *id;
}

%token <d> NUMBER
%token EOL
%token <id> IDENT

%right '='
%left '+' '-'
%left '*' '/'
%nonassoc UMINUS

%type <a> exp

%%
calclist:
        | calclist exp EOL      { auto result = $2->eval(env);
                                  if (result.is_num())
                                      std::cout << "= ";
                                  std::cout << result << std::endl;
                                  delete $2;
                                  prompt();
                                }
        | calclist EOL          { prompt(); }
        | calclist error EOL    { yyerrok; prompt(); }
        ;

exp: IDENT '=' exp              { $$ = new assign_node(*$1, $3); delete $1; }
   | exp '+' exp                { $$ = new op_node(op_type::PLUS, $1, $3); }
   | exp '-' exp                { $$ = new op_node(op_type::MINUS, $1, $3); }
   | exp '*' exp                { $$ = new op_node(op_type::MUL, $1, $3); }
   | exp '/' exp                { $$ = new op_node(op_type::DIV, $1, $3); }
   | '|' exp '|'                { $$ = new op_node(op_type::ABS, $2); }
   | '(' exp ')'                { $$ = $2; }
   | '-' exp %prec UMINUS       { $$ = new op_node(op_type::UMINUS, $2); }
   | NUMBER                     { $$ = new num_node($1); }
   | IDENT                      { $$ = new var_node(*$1); delete $1; }
   ;

%%

