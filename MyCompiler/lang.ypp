%code requires {
#include "ast.h"
#include "parse.h"
}

%{
#include <cstdio>
#include <iostream>
#include <limits>
extern int yylex();
%}

%defines

%union {
    ast_node *a;
    double d;
    std::string *id;
    vector<ast_node*> *enl;
}

%token <d> NUMBER
%token EOL
%token <id> IDENT
%token INF

%right '='
%left '+' '-'
%left '*' '/'
%nonassoc UMINUS

%type <a> exp
%type <enl> exp_list

%%
calclist:
        | calclist exp EOL      { auto result = $2->eval(env);
                                  if (result.is_num())
                                      std::cout << "= ";
                                  std::cout << result << std::endl;
                                  delete $2;
                                  prompt();
                                }
        | calclist EOL          { prompt(); }
        | calclist error EOL    { yyerrok; prompt(); }
        ;

exp: IDENT '=' exp              { $$ = new assign_node(*$1, $3); delete $1; }
   | IDENT '(' exp_list ')'     { $$ = new fun_call_node(*$1, std::move(*$3)); delete $1; }
   | exp '+' exp                { $$ = new op_node(op_type::PLUS, $1, $3); }
   | exp '-' exp                { $$ = new op_node(op_type::MINUS, $1, $3); }
   | exp '*' exp                { $$ = new op_node(op_type::MUL, $1, $3); }
   | exp '/' exp                { $$ = new op_node(op_type::DIV, $1, $3); }
   | '|' exp '|'                { $$ = new op_node(op_type::ABS, $2); }
   | '(' exp ')'                { $$ = $2; }
   | '-' exp %prec UMINUS       { $$ = new op_node(op_type::UMINUS, $2); }
   | NUMBER                     { $$ = new num_node($1); }
   | INF                        { $$ = new num_node(std::numeric_limits<double>::infinity()); }
   | IDENT                      { $$ = new var_node(*$1); delete $1; }
   ;

exp_list: exp                   { $$ = new vector<ast_node*> {$1}; }
        | exp_list ',' exp      { $1->push_back($3); $$ = $1; }
        ;

%%

